# Copyright 2013 The Flutter Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//common/config.gni")
import("//impeller/tools/impeller.gni")
import("//shell/config.gni")

gpu_common_deps = [
  "//common",
  "//common/graphics",
  "//flow",
  "//fml",
  "//shell/common",
  "//skia",
]

source_set("gpu_surface_software") {
  sources = [
    "gpu_surface_software.cc",
    "gpu_surface_software.h",
    "gpu_surface_software_delegate.cc",
    "gpu_surface_software_delegate.h",
  ]

  public_deps = gpu_common_deps
}

source_set("gpu_surface_gl") {
  sources = [
    "gpu_surface_gl_delegate.cc",
    "gpu_surface_gl_delegate.h",
    "gpu_surface_gl_skia.cc",
    "gpu_surface_gl_skia.h",
  ]

  public_deps = gpu_common_deps

  if (impeller_enable_opengles) {
    sources += [
      "gpu_surface_gl_impeller.cc",
      "gpu_surface_gl_impeller.h",
    ]

    public_deps += [ "//impeller" ]
  }
}

source_set("gpu_surface_vulkan") {
  sources = [
    "gpu_surface_vulkan.cc",
    "gpu_surface_vulkan.h",
    "gpu_surface_vulkan_delegate.cc",
    "gpu_surface_vulkan_delegate.h",
  ]
  public_deps = gpu_common_deps + [
                  "//shell/platform/embedder:embedder_headers",
                  "//vulkan",
                  "//vulkan/procs",
                ]

  if (impeller_enable_vulkan) {
    sources += [
      "gpu_surface_vulkan_impeller.cc",
      "gpu_surface_vulkan_impeller.h",
    ]

    public_deps += [ "//impeller" ]
  }
}

source_set("gpu_surface_metal") {
  sources = [
    "gpu_surface_metal_delegate.cc",
    "gpu_surface_metal_delegate.h",
    "gpu_surface_metal_skia.h",
    "gpu_surface_metal_skia.mm",
  ]

  public_deps = gpu_common_deps

  if (impeller_enable_metal) {
    sources += [
      "gpu_surface_metal_impeller.h",
      "gpu_surface_metal_impeller.mm",
    ]

    public_deps += [ "//impeller" ]
  }
}

if (is_mac) {
  impeller_component("gpu_surface_metal_unittests") {
    testonly = true
    target_type = "executable"
    sources = [ "gpu_surface_metal_impeller_unittests.mm" ]

    frameworks = [
      "AppKit.framework",
      "QuartzCore.framework",
    ]

    deps = [
             "//testing",
             ":gpu_surface_metal",
             "//impeller/fixtures",
             "//fml",
             "//runtime",
             "//runtime:libdart",
             "//testing",
           ] + gpu_common_deps
  }
}
